@ECHO OFF

IF [%ROOTDIR%] EQU [] (
    CALL setenv.cmd
)
IF [%ROOTDIR%] EQU [] (
    ECHO ERROR: No root directory defined. Skipping clean.
    EXIT /b 1
)

IF EXIST "%DISTDIR_X86%" (
    ECHO Removing directory "%DISTDIR_X86%"...
    RMDIR /S /Q "%DISTDIR_X86%"
)
IF EXIST "%DISTDIR_X64%" (
    ECHO Removing directory "%DISTDIR_X64%"...
    RMDIR /S /Q "%DISTDIR_X64%"
)
IF EXIST "%OUTPUTDIR_X86%" (
    ECHO Removing directory "%OUTPUTDIR_X86%"...
    RMDIR /S /Q "%OUTPUTDIR_X86%"
)
IF EXIST "%OUTPUTDIR_X64%" (
    ECHO Removing directory "%OUTPUTDIR_X64%"...
    RMDIR /S /Q "%OUTPUTDIR_X64%"
)
IF EXIST "%INCLUDESDIR%\version.h" (
    ECHO Removing "%INCLUDESDIR%\version.h" generated by build process.
    DEL "%INCLUDESDIR%\version.h"
)

SET FOUND_RELEASE_ZIPS=
FOR %%a IN ("%ROOTDIR%\%PROJECT_NAME%-*.zip") DO (
    SET FOUND_RELEASE_ZIPS=1
    GOTO End_Of_Loop
)
:End_Of_Loop
IF DEFINED FOUND_RELEASE_ZIPS (
    ECHO Deleting release zip files...
    DEL "%ROOTDIR%\%PROJECT_NAME%-*.zip"
)

EXIT /b 0

